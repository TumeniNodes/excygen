Execute('../bootstrap')

env = Environment(CPPPATH = ['.', '../bootstrapped/Catch/include/'],
                  CXXFLAGS = "-std=c++0x -Wall -O3 -ffast-math -march=native -msse2 -fopenmp "
                           + "-I/usr/include/python2.7 -I/usr/include/i386-linux-gnu/python2.7 ", # python-config --include`
                  #CXXFLAGS="-std=c++0x -Wall -g -O0 -fopenmp `python-config --include`",
                  CPPFLAGS=""
                  )

py_bindings = env.SharedLibrary(target='py_bindings',
                                SHLIBPREFIX='', # so the bindings so/dll's name is just 'py_bindings'
                                source=['Scripting/Python/py_bindings.cc'],
                                LIBS=['boost_python', 'noise',
                                      'pthread', 'dl', 'util', 'm', 'python2.7' # python-config --libs
                                ])

excygen = env.Program(target='excygen',
                source=['main.cc',
                        'main_test.cc',
                        'PythonAPI.cc',
                        'ImageFormat/PPM.cc',
                        'Photometry/SPD/Regular.cc',
                        'Photometry/SPD/Constant.cc',
                        'Photometry/Spectrum.cc',
                        'Primitives/BoundingIntervalHierarchy.cc',
                        'Shapes/BoundingIntervalHierarchy.cc',
                        'Geometry/Angle.cc',
                        'Geometry/Transform.cc',
                        'Geometry/Matrix44.cc',
                        'DebugPixel.cc',
                        'Shapes/Terrain2d.cc',
                        'Scripting/Et1.cc',
                        'Scripting/Et1/Token.cc',
                        'Scripting/Et1/AST.cc',
                        'Scripting/Et1/ASTAlgorithm.cc',
                        'Scripting/Et1/ASTPasses/0900_unnest_letins.cc',
                        'Scripting/Et1/ASTPasses/1000_lambda_lift.cc',
                        'Scripting/Et1/ASTPasses/1100_resolve_types.cc',
                        'Scripting/Et1/ASTPasses/1150_mangle.cc',
                        'Scripting/Et1/ASTPasses/1200_globalize_functions.cc',
                        'Scripting/Et1/ASTPasses/0500_lift_identifiers_to_calls.cc',
                        'Scripting/Et1/ASTQueries/equal.cc',
                        'Scripting/Et1/ASTQueries/find_references.cc',
                        'Scripting/Et1/ASTQueries/find_binding_names.cc',
                        'Scripting/Et1/ASTQueries/resolve_type.cc',
                        'Scripting/Et1/ASTQueries/has_unresolved.cc',
                        'Scripting/Et1/Backends/PrettyPrinter.cc',
                        'Scripting/Et1/Backends/Python.cc',
                        'Scripting/Et1/UnitTesting.cc',
                        'Scripting/Python.cc'
                       ],
                LIBS=['gomp', 'SDL', 'SDL_image',
                      'pthread', 'dl', 'util', 'm', 'python2.7', 'boost_python' # python-config --libs
                     ]
                )

Default(excygen)
Depends(excygen, py_bindings)

def PhonyTarget(target, action):
    import os
    phony = Environment(ENV = os.environ,
                        BUILDERS = { 'phony' : Builder(action = action) })
    AlwaysBuild(phony.phony(target = target, source = 'SConstruct'))

PhonyTarget('test', './excygen test')
Depends('test', excygen)
